from "animation" import all
from "html" import all

type Login =
    username: string
    password: string
    incorrect: bool

def login-input[T](password: bool, model: string, _place-holder: string, f: |T,Event| do T, a: Atom[T]) VNode =
    let _password = (kind "password")
    let nothing = (style "")

    let isPassword =
        if password then _password else nothing

    div [], [
        h2 [style "text-align: left; margin-bottom: 0px; color: #454545"], _place-holder
        input [
            class "login-input"
            on-input f, a
            value model
            isPassword
        ], ""
        br [], ""
    ]

let change-username = |model: Login, ev: Event| do model{username = ev.target.value, incorrect=false}
let change-password = |model: Login, ev: Event| do model{password = ev.target.value, incorrect=false}

let check-login = |model: Login, ev: Event| do model{password="", incorrect=true}

def login(model: Login, a: Atom[Login]) VNode =
    let isActive = (model.username != "" and model.password != "" and not model.incorrect)

    let toggle = if isActive then "opacity: 1;" else "opacity: 0.5;"

    let incorrect = if model.incorrect then animated + " " +shake else "" //fix if

    div [class "login"], [
        h1 [style "padding-top: 5px;"], "Login or Register"
        p [style "text-align: left; color: #454545"], "If you are already have an account, please sign in. Otherwise, enter your desired username and password and Register."
        div [class incorrect], [
            login-input false, model.username, "Username", change-username, a
            login-input true, model.password, "Password", change-password, a
            br [], ""
        ]
        button [
            class "login-button"
            style toggle
            on-click check-login, a
            disabled (not isActive)
        ], "Submit"
        button [style "color: #2E79B8; font-size: 15px; background: rgba(0,0,0,0); border: 0px;"], "Register"
    ]

start-app login, new-atom Login{"", "",false}