from "modelSettings" import all
import "ui"
import "input"
import "key"
import "editor"
import "model"
import "ecs"
import "layermask"
import "window"
import "vfs"
from "transform" import all
import "events"
import "window"
import "texture"
import "debugDraw"
import "camera"
from "math" import Vec3

type ModelAssetTabEvents either
    SelectedModel(uint)

type ModelAssetTab =
    ui_mesg_queue: events.MesgQueue[ModelAssetTabEvents]
    selected_id: int
    filter: string

def make_ModelAssetTab() ModelAssetTab =
    ModelAssetTab{
        ui_mesg_queue = events.make_MesgQueue::[ModelAssetTabEvents] 10
        filter = ""
        selected_id = -1
    }

def ModelAssetTab.update(&self) =
    edit := editor.get_editor!
    win := window.get_window!

    for mesg := self.ui_mesg_queue.messages do
        match mesg with
            SelectedModel id ->
                log "selected model"
                log id
                self.selected_id = id
                edit.select id

    self.ui_mesg_queue.clear!

    for file := win.dropped_filepaths do
        log "dropped filename: {file}"
        new_context := *context
        new_context.allocator = context.longterm_storage

        #pushContext new_context do
            match vfs.to_asset_path file with
                Some x -> file = x
                None ->
                    log "file {file} not in assets folder"
                    continue

            if (file.ends_with ".fbx") or file.ends_with ".obj" then
                id := ecs.make_ID!

                model_setting := make_ModelSetting id, file

                model_setting.load!

                edit.select id
                self.selected_id = id
            elif (file.ends_with ".png") or (file.ends_with ".JPG") or file.ends_with ".jpg" then
                editor.handle_dropped_image file
            elif file.ends_with ".vert" then
                editor.handle_dropped_shader file
            else
                log "Unknown file format {file}"

    if input.key_pressed key.from_char `N` then
        if self.selected_id < 0 then return

        id := ecs.make_ID!
        entity := ecs.make_Entity id

        edit.select id

        guard Some prefab := prefabs.by_id (self.selected_id cast uint)
        else return

        if Some trans := transforms.by_id (self.selected_id cast uint) then
            cam := camera.get_camera layermask.make_Layermask!.enable layermask.editor_layer
            if Some cam_trans := transforms.by_id camera.cameras.id_of cam then
                new_trans := transforms.make id
                *new_trans = (*trans)
                new_trans.position = cam_trans.position + (cam_trans.rotation.mul_vec3 Vec3{0,0,-10})

        prefab.children.append id

def ModelAssetTab.render_ui(&self, position: ui.Position) =
    editor_tab_width := 0.6
    editor_tab_height := 0.3

    w := ui.window "Place"
    w.style.width = ui.Perc editor_tab_width
    w.style.height = ui.Perc editor_tab_height
    w.position = position

    inp := ui.input w, 50, &self.filter
    inp.style.width = ui.Perc 1

    for model_setting := prefabs.get_all_active! do
        id := prefabs.id_of model_setting

        if not model_setting.name.starts_with self.filter then continue

        selected := id == self.selected_id


        panel := ui.group w, model_setting.name
        panel.style.width = ui.Px 328
        panel.style.height = ui.Px 405

        if Some setting := model_settings_system.by_id id then
            guard Some preview_id := setting.preview_texture
            else continue

            img := ui.image panel, preview_id
            img.style.width = ui.Px 328
            img.style.height = ui.Px 328
            img.ev_handler = Some events.dispatch &self.ui_mesg_queue, ui.Pressed, SelectedModel id
            img.selected = selected
        else
            img := ui.button panel, "select"
            img.style.width = ui.Px 328
            img.style.height = ui.Px 328
            img.ev_handler = Some events.dispatch &self.ui_mesg_queue, ui.Pressed, SelectedModel id
            img.selected = selected