ext _ = "#define STB_IMAGE_IMPLEMENTATION"
ext _ = '#include "stb_image.h"'

import "stbi"
import "assetManager"
from "opengl" import all
import "shader"
import "vfs"
import "ecs"
import "layermask"

type TextureID is uint

type Texture =
    filename: string
    texture_id: TextureID

def make_TextureStore() ecs.Store[Texture] =
    ecs.make_Store_with_init 50, Texture{"", 0}, Some load_in_place

def make_Texture(world: &ecs.World, id: ecs.ID) &Texture =
    entity := match world.entity.by_id id with
        Some x -> x
        None -> world.entity.make id

    entity.layermask = layermask.make_Layermask!
        .enable layermask.serialize_layer

    world.texture.make id

def Texture.get_filename(&self) string =
    self.filename

def Texture.dont_serialize(&self, world: &ecs.World) =
    guard Some entity := world.entity.by_id ecs.id_of self
    else panic "Trying to make texture not serialize, however could not find entity"

    entity.layermask = entity.layermask.disable layermask.serialize_layer

def load_in_place(world: &ecs.World, tex: &Texture) =
    log "loading in place {tex.filename}"
    filename := tex.filename
    real_filename := vfs.asset_path tex.filename

    stbi.set_flip_vertically_on_load true

    width : int = 0
    height : int = 0
    nr_channels : int = 0
    texture_id := 0

    genTextures 1, &texture_id
    bindTexture opengl.texture_2D, texture_id

    texParameteri texture_2D, texture_wrap_s, repeat
    texParameteri texture_2D, texture_wrap_t, repeat
    texParameteri texture_2D, texture_min_filter, linear_mipmap_linear
    texParameteri texture_2D, texture_mag_filter, linear
    texParameterf texture_2D, texture_max_anisotropy_ext, 16

    guard Some data := stbi.load real_filename.to_c_string!, &width, &height, &nr_channels, 3
    else panic "Could not load image {filename}"

    nr_channels = 3

    internal_color_format :=
        if nr_channels == 1 then red
        elif nr_channels == 2 then rg
        elif nr_channels == 4 then rgba
        else rgb

    if nr_channels != 3 then
        log nr_channels

    texImage2D texture_2D, 0, rgba, width, height, 0, internal_color_format, unsigned_byte, Some data
    generateMipmap texture_2D

    stbi.image_free data

    tex.texture_id = texture_id

def load(world: &ecs.World, filename: string) &Texture =
    if Some texture := assetManager.in_cache &world.texture, world, filename then
        return texture

    texture := make_Texture world, world.make_ID!
    texture.filename = filename

    load_in_place world, texture

    texture

def Texture.bind_to(self, num: uint) =
    //log num
    activeTexture texture0 + num
    bindTexture opengl.texture_2D, self.texture_id

type Cubemap =
    id: uint

def Cubemap.bind_to(self, num: uint) =
    //log num

    activeTexture texture0 + num
    bindTexture opengl.texture_cube_map, self.id

