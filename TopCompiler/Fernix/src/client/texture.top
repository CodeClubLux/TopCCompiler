ext _ = "#define STB_IMAGE_IMPLEMENTATION"
ext _ = '#include "stb_image.h"'

import "stbi"
import "assetManager"
from "opengl" import all
import "shader"
import "vfs"
import "ecs"
import "layermask"

type TextureID is uint

type Texture =
    entity: &ecs.Entity
    filename: string
    texture_id: TextureID

textures_system := ecs.make_Store make_Texture, 20

def make_Texture(id: ecs.ID) &Texture =
    entity := match ecs.entity_system.component_by_id id with
        Some x -> x
        None -> ecs.make_Entity id

    entity.layermask = layermask.make_Layermask!

    textures_system.add_component Texture{
        entity = entity
        texture_id = 0
        filename = ""
    }

def Texture.render(&self) =
def Texture.update(&self) =
def Texture.get_entity(&self) &ecs.Entity =
    self.entity

def Texture.get_filename(&self) string =
    self.filename


def load(filename: string) &Texture =
    real_filename := vfs.asset_path filename

    match assetManager.in_cache textures_system, filename with
        Some texture -> return texture
        None ->

    stbi.set_flip_vertically_on_load true

    width : int = 0
    height : int = 0
    nr_channels : int = 0
    texture_id := 0

    genTextures 1, &texture_id
    bindTexture opengl.texture_2D, texture_id

    texParameteri texture_2D, texture_wrap_s, repeat
    texParameteri texture_2D, texture_wrap_t, repeat
    texParameteri texture_2D, texture_min_filter, linear_mipmap_linear
    texParameteri texture_2D, texture_mag_filter, linear

    data := match stbi.load real_filename.to_c_string!, &width, &height, &nr_channels, 0 with
        Some d -> d
        None ->
            panic "Could not load image {filename}"
            0 cast &Byte

    defer stbi.image_free data

    internal_color_format :=
        if nr_channels == 1 then red
        elif nr_channels == 2 then rg
        else rgb

    if nr_channels != 3 then
        log nr_channels

    texImage2D texture_2D, 0, rgb, width, height, 0, internal_color_format, unsigned_byte, Some data
    generateMipmap texture_2D

    texture := make_Texture ecs.make_ID!
    texture.filename = filename
    texture.texture_id = texture_id

    texture

def Texture.bind_to(self, num: uint) =
    activeTexture texture0 + num
    bindTexture opengl.texture_2D, self.texture_id

type Cubemap =
    id: uint

def Cubemap.bind_to(self, num: uint) =
    activeTexture texture0 + num
    bindTexture opengl.texture_cube_map, self.id

