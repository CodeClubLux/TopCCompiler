type Mat4x4 is [16]float

def empty_mat() Mat4x4 =
    [16: 0.0]

def identity_mat() Mat4x4 =
    new := empty_mat!
    new[0] = 1
    new[5] = 1
    new[10] = 1
    new[15] = 1

    new

def Mat4x4.op_mul(a, b: Mat4x4) Mat4x4 =
    r : Mat4x4 = _

    r[0] = a[0] * b[0] + a[1] * b[4] + a[2] * b[8] + a[3] * b[12]
    r[1] = a[0] * b[1] + a[1] * b[5] + a[2] * b[9] + a[3] * b[13]
    r[2] = a[0] * b[2] + a[1] * b[6] + a[2] * b[10] + a[3] * b[14]
    r[3] = a[0] * b[3] + a[1] * b[7] + a[2] * b[11] + a[3] * b[15]

    r[4] = a[4] * b[0] + a[5] * b[4] + a[6] * b[8] + a[7] * b[12]
    r[5] = a[4] * b[1] + a[5] * b[5] + a[6] * b[9] + a[7] * b[13]
    r[6] = a[4] * b[2] + a[5] * b[6] + a[6] * b[10] + a[7] * b[14]
    r[7] = a[4] * b[3] + a[5] * b[7] + a[6] * b[11] + a[7] * b[15]

    r[8] = a[8] * b[0] + a[9] * b[4] + a[10] * b[8] + a[11] * b[12]
    r[9] = a[8] * b[1] + a[9] * b[5] + a[10] * b[9] + a[11] * b[13]
    r[10] = a[8] * b[2] + a[9] * b[6] + a[10] * b[10] + a[11] * b[14]
    r[11] = a[8] * b[3] + a[9] * b[7] + a[10] * b[11] + a[11] * b[15]

    r[12] = a[12] * b[0] + a[13] * b[4] + a[14] * b[8] + a[15] * b[12]
    r[13] = a[12] * b[1] + a[13] * b[5] + a[14] * b[9] + a[15] * b[13]
    r[14] = a[12] * b[2] + a[13] * b[6] + a[14] * b[10] + a[15] * b[14]
    r[15] = a[12] * b[3] + a[13] * b[7] + a[14] * b[11] + a[15] * b[15]

    r

def Mat4x4.mul_vec4(&m, vec: Vec4) Vec4 =
    new_vec := [4: 0.0]

    for i := 0..4 do
        for j := 0..4 do
            new_vec[j] += m[i*4 + j] * vec[i]

    vec4_from_array &new_vec

def rotate(q: Quat) Mat4x4 =
    a := q.w
    b := q.x
    c := q.y
    d := q.z
    a2 := a * a
    b2 := b * b
    c2 := c * c
    d2 := d * d

    [
        (a2 + b2 - c2 - d2), 2*(b*c - a*d), 2*(b*d + a*c) , 0
        2*(b*c + a*d)      , (a2-b2+c2-d2), 2*(c*d - a*b) , 0
        2*(b*d - a*c)      , 2*(c*d + a*b), (a2-b2-c2+d2) , 0
        0                  , 0            , 0             , 1
    ]


def translate(position: Vec3) Mat4x4 =
    [
        1, 0, 0, position.x
        0, 1, 0, position.y
        0, 0, 1, position.z
        0, 0, 0, 1
    ]

def scale(s: Vec3) Mat4x4 =
    [
        s.x, 0   , 0  , 0
        0  , s.y , 0  , 0
        0  , 0   , s.z, 0
        0  , 0   , 0  , 1
    ]


def perspective(fov: float, aspect: float, n: float, f: float) Mat4x4 =
    a := 1.0 / tan fov / 2.0

    [
        (a / aspect) ,  0  ,   0                   , 0
        0          ,    a   ,  0                   , 0
        0          ,    0   , -((f + n) / (f - n)) , -((2 * f * n) / (f - n))
        0          ,    0   ,   -1                 , 0
    ]

def look_at(eye: Vec3, center: Vec3, up: Vec3) Mat4x4 =
    f := (center - eye).norm!
    s := (f.cross up).norm!

    t := s.cross f

    [
        s.x, s.y, s.z, -eye.x
        t.x, t.y, t.z, -eye.y
        -f.x,-f.y,-f.z, -eye.z
        0 ,  0   , 0,   1
    ]



//tmp_mat := make_mat Vec3{0,0,0}, identity_quat!, Vec3{1,1,1}
//
//for tmp_i := 0..16 do
//    log tmp_mat[tmp_i]

def invert(m: Mat4x4) Mat4x4 =
    invOut : Mat4x4 = _
    inv := [16: 0.0]

    det := 0.0

    inv[0] = m[5]  * m[10] * m[15] - m[5]  * m[11] * m[14] - m[9]  * m[6]  * m[15] + m[9]  * m[7]  * m[14] + m[13] * m[6]  * m[11] - m[13] * m[7]  * m[10]
    inv[4] = -m[4]  * m[10] * m[15] + m[4]  * m[11] * m[14] + m[8]  * m[6]  * m[15] - m[8]  * m[7]  * m[14] - m[12] * m[6]  * m[11] + m[12] * m[7]  * m[10]
    inv[8] = m[4]  * m[9] * m[15] - m[4]  * m[11] * m[13] - m[8]  * m[5] * m[15] + m[8]  * m[7] * m[13] + m[12] * m[5] * m[11] - m[12] * m[7] * m[9]
    inv[12] = -m[4]  * m[9] * m[14] + m[4]  * m[10] * m[13] + m[8]  * m[5] * m[14] - m[8]  * m[6] * m[13] - m[12] * m[5] * m[10] + m[12] * m[6] * m[9]
    inv[1] = -m[1]  * m[10] * m[15] + m[1]  * m[11] * m[14] + m[9]  * m[2] * m[15] - m[9]  * m[3] * m[14] - m[13] * m[2] * m[11] + m[13] * m[3] * m[10]
    inv[5] = m[0]  * m[10] * m[15] - m[0]  * m[11] * m[14] - m[8]  * m[2] * m[15] + m[8]  * m[3] * m[14] + m[12] * m[2] * m[11] - m[12] * m[3] * m[10]
    inv[9] = -m[0]  * m[9] * m[15] + m[0]  * m[11] * m[13] + m[8]  * m[1] * m[15] - m[8]  * m[3] * m[13] - m[12] * m[1] * m[11] + m[12] * m[3] * m[9]
    inv[13] = m[0]  * m[9] * m[14] - m[0]  * m[10] * m[13] - m[8]  * m[1] * m[14] + m[8]  * m[2] * m[13] + m[12] * m[1] * m[10] - m[12] * m[2] * m[9]
    inv[2] = m[1]  * m[6] * m[15] - m[1]  * m[7] * m[14] - m[5]  * m[2] * m[15] + m[5]  * m[3] * m[14] + m[13] * m[2] * m[7] - m[13] * m[3] * m[6]
    inv[6] = -m[0]  * m[6] * m[15] + m[0]  * m[7] * m[14] + m[4]  * m[2] * m[15] - m[4]  * m[3] * m[14] - m[12] * m[2] * m[7] + m[12] * m[3] * m[6]
    inv[10] = m[0]  * m[5] * m[15] - m[0]  * m[7] * m[13] - m[4]  * m[1] * m[15] + m[4]  * m[3] * m[13] + m[12] * m[1] * m[7] - m[12] * m[3] * m[5]
    inv[14] = -m[0]  * m[5] * m[14] + m[0]  * m[6] * m[13] + m[4]  * m[1] * m[14] - m[4]  * m[2] * m[13] - m[12] * m[1] * m[6] + m[12] * m[2] * m[5]
    inv[3] = -m[1] * m[6] * m[11] + m[1] * m[7] * m[10] + m[5] * m[2] * m[11] - m[5] * m[3] * m[10] - m[9] * m[2] * m[7] + m[9] * m[3] * m[6]
    inv[7] = m[0] * m[6] * m[11] - m[0] * m[7] * m[10] - m[4] * m[2] * m[11] + m[4] * m[3] * m[10] + m[8] * m[2] * m[7] - m[8] * m[3] * m[6]
    inv[11] = -m[0] * m[5] * m[11] + m[0] * m[7] * m[9] + m[4] * m[1] * m[11] - m[4] * m[3] * m[9] - m[8] * m[1] * m[7] + m[8] * m[3] * m[5]
    inv[15] = m[0] * m[5] * m[10] - m[0] * m[6] * m[9] - m[4] * m[1] * m[10] + m[4] * m[2] * m[9] + m[8] * m[1] * m[6] - m[8] * m[2] * m[5]

    det = m[0] * inv[0] + m[1] * inv[4] + m[2] * inv[8] + m[3] * inv[12]

    if det == 0 then
        return invOut

    det = 1.0 / det;

    for i := 0..16 do
        invOut[i] = inv[i] * det

    invOut

test := (invert perspective 56, 1, 0.1, 100) * (invert translate Vec3{1,0,0})
test2 := (&test).mul_vec4 Vec4{0,0,0,1}
test2 = test2.scale (1/test2.w)

//test = invert test
//test2 = (&test).mul_vec4 test2
//test2 = test2.scale (1/test2.w)

log test2

log "==== vec3"